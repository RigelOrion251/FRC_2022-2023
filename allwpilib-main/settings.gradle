pluginManagement {
    repositories {
        mavenLocal()
        maven {
            url = 'https://frcmaven.wpi.edu/artifactory/ex-gradle'
        }
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "com.gradle.enterprise" version "3.0"
}

// Set the flag to tell gradle to ignore unresolved headers
// Libraries like eigen and opencv use macro includes, which
// Gradle doesn't properly ignore, and completely disables
// Incremental includes. This flag makes those includes be ignored.
Properties props = System.getProperties();
props.setProperty("org.gradle.internal.native.headers.unresolved.dependencies.ignore", "true");

include 'wpiutil'
include 'wpinet'
include 'ntcore'
include 'hal'
include 'cscore'
include 'wpigui'
include 'wpimath'
include 'wpilibc'
include 'wpilibcExamples'
include 'wpilibcIntegrationTests'
include 'wpilibjExamples'
include 'wpilibjIntegrationTests'
include 'wpilibj'
include 'crossConnIntegrationTests'
include 'fieldImages'
include 'glass'
include 'outlineviewer'
include 'roborioteamnumbersetter'
include 'datalogtool'
include 'simulation:halsim_ds_socket'
include 'simulation:halsim_gui'
include 'simulation:halsim_ws_core'
include 'simulation:halsim_ws_client'
include 'simulation:halsim_ws_server'
include 'cameraserver'
include 'cameraserver:multiCameraServer'
include 'wpilibNewCommands'
include 'myRobot'
include 'docs'
include 'msvcruntime'
include 'ntcoreffi'
include 'apriltag'

buildCache {
    def cred = {
        if (System.env[it] != null) {
            return System.env[it]
        } else {
            return System.getProperty(it)
        }
    }
    local {
        enabled = !System.getenv().containsKey("CI")
    }
    remote(HttpBuildCache) {
        url = "https://frcmaven.wpi.edu/artifactory/wpilib-generic-gradle-cache/"
        String user = cred('ARTIFACTORY_PUBLISH_USERNAME')
        String pass = cred('ARTIFACTORY_PUBLISH_PASSWORD')
        if (user && pass) {
            push = true
            credentials {
                username = user
                password = pass
            }
        }
    }
}
